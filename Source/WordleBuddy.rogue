#================================================================================
# WordleBuddy.rogue
# January 5, 2022
#================================================================================
$define VERSION "2.1"
$define DATE    "October 29, 2023"

$requireRogue "2.0.8"

$include "Dictionary.rogue"
$include "ExtendedDictionary.rogue"
$include "Game.rogue"
$include "GameState.rogue"
$include "Letters.rogue"
$include "MainDictionary.rogue"
$include "Solver.rogue"
$include "WordList.rogue"

uses Utility/Best
uses Utility/CommandLineParser

try
  WordleBuddy( System.command_line_arguments.cloned )
catch (error:Error)
  Console.error.println error
  System.exit 1
endTry

class WordleBuddy
  # Uses "official" Wordle word lists courtesy of twitch.tv/graham
  #   https://observablehq.com/@iamgrahamallen/wordle-solver-exploration
  #   https://observablehq.com/@iamgrahamallen/wordle-solver-exploration-part-2
  GLOBAL PROPERTIES
    use_extended_word_list = false

  METHODS
    method init( args:String[] )
      local command = parse_args( args )

      if (command.is_undefined)
        print_usage
        System.exit 0
      endIf

      if (command//list)
        local letters : String
        if (command//letters) letters = command//letters->String
        list( command//pattern, letters )

      elseIf (command//game)
        Game( &solver=command//solver? ).play

      else
        Solver().play

      endIf

    method list( pattern:String, letters:String )
      local gamestate = GameState()
      gamestate.filter( pattern )
      if (letters)
        gamestate.candidates.remove_words_not_containing( forEach in letters.to_lowercase )
      endIf
      println (forEach in gamestate.candidates)

    method parse_args( args:String[] )->Value
      contingent
        if (args.contains("--extended"))
          args.remove( "--extended" )
          use_extended_word_list = true
        endIf

        if (args.contains("-e"))
          args.remove( "-e" )
          use_extended_word_list = true
        endIf

        if (use_extended_word_list) Dictionary = ExtendedDictionary()
        else                        Dictionary = MainDictionary()

        necessary (args.count)

        if (args.first == "list")
          necessary (args.count == 2 or args.count == 4)
          local pattern = args[1]
          if (pattern.count != 5)
            println ''Pattern must contain 5 characters; "$" contains $ characters.''(pattern,pattern.count)
            return undefined
          endIf

          local result = @{ list:true, :pattern }
          if (args.count == 4)
            necessary (args[2] == "containing")
            result//letters = args[3]
          endIf

          return result
        endIf

        if (args.first == "variations" or args.first == "v")
          necessary (args.count == 2)
          local letters = args[1]
          local pattern = Character[]
          pattern.add( forEach in letters)
          forEach (i in 0..<pattern.permutation_count)
            contingent
              local permutation = String( pattern.permutation(i) )
              forEach (ch at i2 in letters)
                if (ch.is_uppercase)
                  necessary (permutation[i2] == ch)
                endIf
              endForEach
              println permutation.to_uppercase
            endContingent
          endForEach
          System.exit
        endIf

        local result = @{}
        if (args.contains("game"))
          args.remove( "game" )
          result//game = true
        endIf

        if (args.contains("solver"))
          args.remove( "solver" )
          result//solver = true
        endIf

        necessary (args.is_empty)
        return result

      unsatisfied
        return undefined

      endContingent

    method print_usage
      println "WordleBuddy v$"(VERSION)
      println "$ by Brom Bresenham"(DATE)
      println
      println @|USAGE
               |  wordlebuddy [OPTIONS] solver
               |  wordlebuddy [OPTIONS] game
               |  wordlebuddy [OPTIONS] game solver
               |  wordlebuddy [OPTIONS] list <pattern>
               |  wordlebuddy [OPTIONS] list <pattern> containing <letters>
               |  wordlebuddy variations/v letters
               |
               |OPTIONS
               |  --extended, -e
               |    Use the extended word list. Wordle chooses solutions from a smaller word
               |    and includes an extended list of uncommon words that are valid guesses.
               |    The --extended option directs Wordle Buddy to use the extended word list
               |    as possible solutions. "Dordle" seems to pick at least some solutions
               |    from the extended list.
               |
               |DESCRIPTION
               |  wordlebuddy solver
               |    Interactively assists in solving an online Wordle puzzle.
               |
               |  wordlebuddy game
               |    Plays a Wordle game without solver assistance.
               |
               |  wordlebuddy game solver
               |    Plays a Wordle game with solver assistance.
               |
               |  wordlebuddy list "<pattern>"
               |    List possible words that match the given 5-character pattern.
               |
               |    <pattern>
               |      * - Match words with any character at this position.
               |      A - Match words with an 'A' at this position.
               |      a - Match words with an 'A' NOT at this position.
               |
               |  wordlebuddy list "<pattern>" containing <letters>
               |    Lists words matching the pattern (which can be "*****") AND
               |    containing all the given letters.
               |
               |  wordlebuddy variations <pattern>, wordlebuddy v <pattern>
               |    Lists all possible variations (permutations) of the given lowercase letters.
               |    Uppercase letters are locked in place.
               |
               |EXAMPLES
               |  wordlebuddy list "r*O*T"
               |    # List words that have 'O' in the middle, end in 'T', and
               |    # contain an 'R' but don't start with it.
               |
               |  wordlebuddy list "*O**c" containing tu
               |    # List words that have O as the second letter, contain 'C'
               |    # but not as the last letter, and contain the letters 'T'
               |    # and 'U' anywhere.

endClass

